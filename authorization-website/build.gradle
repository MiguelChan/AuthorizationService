buildscript {
    repositories {
        gradlePluginPortal()
    }

    dependencies {
        classpath "com.github.node-gradle:gradle-node-plugin:3.1.1"
    }
}

apply plugin: 'com.github.node-gradle.node';
apply plugin: 'base';

String testsExecutedMarkerName = "${projectDir}/.tests.executed";

// Here we configure the current NodeJS version.
node {
    // We set current NodeJS Version.
    version = '12.2.0';
    // We set current NPM Version.
    npmVersion = '7.8.0';
    // We tell the plug-in whether we should download the NodeJS Binaries or not.
    download = true;
}

npm_run_build {
    inputs.files fileTree("public")
    inputs.files fileTree("src")

    inputs.file 'package.json'
    inputs.file 'package-lock.json'

    outputs.dir 'build'
}

task packageNpmApp(type: Zip) {
    dependsOn npm_run_build;
    baseName 'authorization-website';
    extension 'jar';
    destinationDir file("${projectDir}/build_packageNpmApp");
    from('build') {
        into 'static';
    };
}

configurations {
    npmResources
}

configurations.default.extendsFrom(configurations.npmResources);

artifacts {
    npmResources(packageNpmApp.archivePath) {
        builtBy packageNpmApp
        type 'jar';
    }
}

assemble.dependsOn packageNpmApp;

clean {
    delete packageNpmApp.archivePath
    delete file("${projectDir}/build_packageNpmApp")
    delete testsExecutedMarkerName
}

tasks.register('release') {
    dependsOn tasks.test
    dependsOn tasks.build
}

task test(type: NpmTask) {
    dependsOn assemble

    args = ['run', 'test', '--', '--watchAll=false']

    inputs.files fileTree('src')
    inputs.file 'package.json'
    inputs.file 'package-lock.json'

    // allows easy triggering re-tests
    doLast {
        new File(testsExecutedMarkerName).text = 'delete this file to force re-execution JavaScript tests'
    }
    outputs.file testsExecutedMarkerName
}

check.dependsOn test
